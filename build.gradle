buildscript {
    ext.kotlin_version = '1.2.61'

    repositories {
        jcenter()
        mavenCentral()
        maven { url "https://dl.bintray.com/kotlin/kotlin-eap" }
        maven { url "https://kotlin.bintray.com/kotlinx" }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:0.9.14"
        classpath "com.pascalwelsch.gitversioner:gitversioner:0.3.1"
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.+'
    }
}

apply plugin: "idea"
apply plugin: "kotlin2js"
apply plugin: "maven-publish"
apply plugin: 'org.jetbrains.dokka'
apply plugin: "com.pascalwelsch.gitversioner"
apply plugin: 'com.jfrog.bintray'

repositories {
    jcenter()
    mavenCentral()
    maven { url "https://kotlin.bintray.com/kotlinx" }
    maven { url "https://dl.bintray.com/danfma/kotlin-kodando" }
}

dependencies {
    compileOnly "org.jetbrains.kotlin:kotlin-stdlib-js:$kotlin_version"
    compileOnly "org.jetbrains.kotlinx:kotlinx-html-js:0.6.8"
    compileOnly "br.danfma.kodando:kodando-history:0.3.0"
    compileOnly "org.jetbrains.kotlinx:kotlinx-coroutines-core-js:0.23.3"
}

group "me.theghostin"
version "1.0.1"

task sourcesJar(type: Jar) {
    classifier = "sources"
    from sourceSets.main.allSource
}

task wrapper(type: Wrapper) {
    // FIXME: while this issue (https://github.com/Kotlin/dokka/issues/265) is fixed in newer dokka plugins
    // newer dokka plugins don't work because of this https://github.com/Kotlin/dokka/issues/212
    gradleVersion = '4.4.1' //version required
}

task cleanDocs(type: Delete) {
    delete "$rootDir/docs"
}

gitVersioner {
    addSnapshot false
}

kotlin {
    experimental {
        coroutines "enable"
    }
}

compileKotlin2Js {
    kotlinOptions.metaInfo = true
    kotlinOptions.outputFile = "$project.buildDir.path/js/${project.name}.js"
    kotlinOptions.moduleKind = 'commonjs'
    kotlinOptions.main = "call"
}

dokka {
    outputFormat = "html"
    outputDirectory = "$rootDir/docs"
    impliedPlatforms = ["JS"]
    // reportUndocumented = false
}
dokka.dependsOn cleanDocs
build.dependsOn dokka

bintray {
    user =  System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['Nimble']
    publish = true
    pkg {
        repo = 'me.theghostin'
        name = 'nimble'
        userOrg = user
        licenses = ['GPL-3.0']
    }
}
publishing {
    publications {
        Nimble(MavenPublication) {
            from components.java
            artifact sourcesJar
        }
    }
}